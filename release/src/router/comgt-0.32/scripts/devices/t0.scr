opengt
set com 460800n81
set senddelay 0.05
waitquiet 1 0.2

system "nvram set g3state_z=0"
system "nvram set g3state_q0=0"
system "nvram set g3state_apn=0"
system "nvram set g3state_dial=0"
system "nvram set g3state_conn=0"


:ready
system "nvram set g3state_z=1"
send "ATZ^m"
waitfor 10 "OK","ERR"
if % = -1 goto timeerror
if % = 0 goto set_parameters
system "nvram set g3state_z=-1"
goto error


:set_parameters
system "nvram set g3state_q0=1"
send "E1 Q0 V1 X4 &C1 &D1 &R1 &S0 &K0 +FCLASS0^m"
waitfor 10 "OK","ERR"
if % = -1 goto timeerror
if % = 0 goto set_apn
system "nvram set g3state_q0=-1"
goto error


:set_apn
system "nvram set g3state_apn=1"
let $x=$rpipe("nvram get modem_apn")
let l=len($x)
if l=0 goto apnerror
if l>32 goto apnerror
send "AT+CGDCONT=1,\"IP\",\""
send $x
send "\"^m"
waitfor 20 "OK","ERR"
if % = -1 goto apntimeerror
if % = 0 goto set_dial
system "nvram set g3state_apn=-1"
goto apnerror


:set_dial
system "nvram set g3state_dial=1"
let $x=$rpipe("nvram get modem_dialnum")
let a=len($x)
if a=0 goto error
send "ATDT"
send $x
send "^m"
waitfor 10 "CONNECT","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto done
system "nvram set g3state_dial=-1"
goto error


:done
system "nvram set g3state_conn=1"
print "CONNECTED\n"
exit 0


:apnerror
system "nvram set g3err_apn=1"
print "ERROR entering APN\n"
print \"The COMGTAPN env variable is not set.\n"
send "ATH^m"
exit 1


:apntimeerror
system "nvram set g3err_apn=2"
print \"ERROR entering APN\n"
print "The device timeout\n"
send "ATH^m"
exit 1


:error
system "nvram set g3err_conn=1"
print "CONNECT ERROR\n"
send "ATH^m"
exit 1


:timeerror
system "nvram set g3err_conn=2"
print "CONNECT TIMEOUT\n"
send "ATH^m"
exit 1
