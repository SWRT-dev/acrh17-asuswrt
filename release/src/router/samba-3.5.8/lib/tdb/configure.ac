AC_PREREQ(2.50)
AC_DEFUN([SMB_MODULE_DEFAULT], [echo -n ""])
AC_DEFUN([SMB_LIBRARY_ENABLE], [echo -n ""])
AC_DEFUN([SMB_ENABLE], [echo -n ""])
AC_INIT(tdb, 1.2.1)
AC_CONFIG_SRCDIR([common/tdb.c])
AC_CONFIG_HEADER(include/config.h)
AC_LIBREPLACE_ALL_CHECKS
AC_LD_SONAMEFLAG
AC_LD_VERSIONSCRIPT
AC_LD_PICFLAG
AC_LD_SHLIBEXT
AC_LIBREPLACE_SHLD
AC_LIBREPLACE_SHLD_FLAGS
AC_LIBREPLACE_RUNTIME_LIB_PATH_VAR
m4_include(libtdb.m4)
AC_PATH_PROGS([PYTHON_CONFIG], [python2.6-config python2.5-config python2.4-config python-config])
AC_PATH_PROGS([PYTHON], [python2.6 python2.5 python2.4 python])

PYTHON_BUILD_TARGET="build-python"
PYTHON_INSTALL_TARGET="install-python"
PYTHON_CHECK_TARGET="check-python"
AC_SUBST(PYTHON_BUILD_TARGET)
AC_SUBST(PYTHON_INSTALL_TARGET)
AC_SUBST(PYTHON_CHECK_TARGET)
if test -z "$PYTHON_CONFIG"; then
	PYTHON_BUILD_TARGET=""
	PYTHON_INSTALL_TARGET=""
	PYTHON_CHECK_TARGET=""
fi

AC_ARG_ENABLE(python,
	AS_HELP_STRING([--enable-python], [Enables python binding]),
		[ if test "x$enableval" = "xno" ; then
			PYTHON_BUILD_TARGET=""
			PYTHON_INSTALL_TARGET=""
			PYTHON_CHECK_TARGET=""
		  fi
		])

AC_PATH_PROG(XSLTPROC,xsltproc)
DOC_TARGET=""
if test -n "$XSLTPROC"; then
	DOC_TARGET=doc
fi
AC_SUBST(DOC_TARGET)

m4_include(build_macros.m4)
BUILD_WITH_SHARED_BUILD_DIR

AC_OUTPUT(Makefile tdb.pc)
